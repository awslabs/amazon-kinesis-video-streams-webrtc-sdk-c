cmake_minimum_required(VERSION 3.6.3)

project(libwebsocket-download NONE)

SET(PATCH_COMMAND git apply --ignore-whitespace ${CMAKE_CURRENT_LIST_DIR}/libwebsockets-old-gcc-fix-cast-cmakelists.patch ${CMAKE_CURRENT_LIST_DIR}/libwebsockets-leak-pipe-fix.patch)

include(ExternalProject)
   
if (BUILD_STATIC_LIBS)
  set(LWS_WITH_STATIC 1)
  set(LWS_WITH_SHARED 0)
else()
  set(LWS_WITH_STATIC 0)
  set(LWS_WITH_SHARED 1)
endif()

if (USE_OPENSSL)
  set(LWS_WITH_MBEDTLS OFF)
else()
  set(LWS_WITH_MBEDTLS ON)
endif()

if (WIN32)
  set(LWS_HAVE_PTHREAD_H 1)
  set(EXT_PTHREAD_INCLUDE_DIR ${LWS_EXT_PTHREAD_INCLUDE_DIR})
  set(EXT_PTHREAD_LIBRARIES ${LWS_EXT_PTHREAD_LIBRARIES})
  set(OPENSSL_INCLUDE_DIRS ${LWS_OPENSSL_INCLUDE_DIRS})
  
  set(OPENSSL_LIBRARIES ${MY_OPENSSL_SSL_LIBRARY} ${MY_OPENSSL_CRYPTO_LIBRARY})
  string(REPLACE ";" "|" OPENSSL_LIBRARIES_ALT_SEP "${OPENSSL_LIBRARIES}")

  set(WITH_THREADPOOL ${LWS_WITH_THREADPOOL})
  set(LWS_WITH_STATIC ON)
endif()

ExternalProject_Add(project_libwebsockets
    GIT_REPOSITORY    https://github.com/warmcat/libwebsockets.git
    GIT_TAG           v4.2.2
    PATCH_COMMAND     ${PATCH_COMMAND}
    PREFIX            ${CMAKE_CURRENT_BINARY_DIR}/build
    LIST_SEPARATOR    |
    CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=${OPEN_SRC_INSTALL_PREFIX}
        -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
        -DLWS_WITH_STATIC=${LWS_WITH_STATIC}
        -DLWS_WITH_SHARED=${LWS_WITH_SHARED}
        -DLWS_WITH_MBEDTLS=${LWS_WITH_MBEDTLS}
        -DLWS_WITH_MINIMAL_EXAMPLES=1
        -DLWS_HAVE_PTHREAD_H=1 
        -DLWS_EXT_PTHREAD_INCLUDE_DIR=${EXT_PTHREAD_INCLUDE_DIR}
        -DLWS_EXT_PTHREAD_LIBRARIES=${EXT_PTHREAD_LIBRARIES}
        -DLWS_OPENSSL_INCLUDE_DIRS=${OPENSSL_INCLUDE_DIRS}
        -DLWS_OPENSSL_LIBRARIES=${OPENSSL_LIBRARIES_ALT_SEP}
    BUILD_ALWAYS      TRUE
    TEST_COMMAND      ""
)
