cmake_minimum_required(VERSION 2.8)

project(libwebsocket-download NONE)

SET(PATCH_COMMAND git apply --ignore-whitespace ${CMAKE_CURRENT_LIST_DIR}/libwebsockets-old-gcc-fix-cast-cmakelists.patch ${CMAKE_CURRENT_LIST_DIR}/libwebsockets-leak-pipe-fix.patch)

include(ExternalProject)

if (BUILD_STATIC_LIBS)
  set(LWS_WITH_STATIC 1)
  set(LWS_WITH_SHARED 0)
else()
  set(LWS_WITH_STATIC 0)
  set(LWS_WITH_SHARED 1)
  if (USE_OPENSSL)
    find_package(OpenSSL REQUIRED)
  endif()
endif()

if (USE_OPENSSL)
  set(LWS_WITH_MBEDTLS OFF)
else()
  set(LWS_WITH_MBEDTLS ON)
endif()

ExternalProject_Add(project_libwebsockets
    GIT_REPOSITORY    https://github.com/warmcat/libwebsockets.git
    GIT_TAG           v4.2.2
    PATCH_COMMAND     ${PATCH_COMMAND}
    PREFIX            ${CMAKE_CURRENT_BINARY_DIR}/build
    CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=${OPEN_SRC_INSTALL_PREFIX}
        -DLWS_WITH_HTTP2=1
        -DLWS_HAVE_HMAC_CTX_new=1
        -DLWS_HAVE_SSL_EXTRA_CHAIN_CERTS=1
        -DLWS_HAVE_OPENSSL_ECDH_H=1
        -DLWS_HAVE_EVP_MD_CTX_free=1
        -DLWS_WITHOUT_SERVER=1
        -DLWS_WITHOUT_TESTAPPS=1
        -DLWS_WITH_THREADPOOL=1
        -DLWS_WITHOUT_TEST_SERVER_EXTPOLL=1
        -DLWS_WITHOUT_TEST_PING=1
        -DLWS_WITHOUT_TEST_CLIENT=1
        -DLWS_WITH_STATIC=${LWS_WITH_STATIC}
        -DLWS_WITH_SHARED=${LWS_WITH_SHARED}
        -DLWS_STATIC_PIC=1
        -DLWS_WITH_ZLIB=0
        -DLWS_HAVE_EVENTFD=0
        -DLWS_WITH_MBEDTLS=${LWS_WITH_MBEDTLS}
# enable for debug output        -DCMAKE_BUILD_TYPE=DEBUG
        -DLWS_OPENSSL_INCLUDE_DIRS=${OPENSSL_INCLUDE_DIR}
        -DLWS_OPENSSL_CRYPTO_LIBRARY=${OPENSSL_CRYPTO_LIBRARY}
        -DLWS_OPENSSL_SSL_LIBRARY=${OPENSSL_SSL_LIBRARY}
        -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
        BUILD_ALWAYS      TRUE
    TEST_COMMAND      ""
)
